#!/usr/bin/env python

Import("env")

env_unicode = env.Clone()

# Thirdparty source files

thirdparty_obj = []

if env["builtin_libunibreak"]:
    thirdparty_dir = "#thirdparty/libunibreak/"
    thirdparty_sources = [
        "src/unibreakbase.c",
        "src/unibreakdef.c",
        "src/linebreakdata.c",
        "src/linebreakdef.c",
        "src/linebreak.c",
        "src/indicconjunctbreakdata.c",
        "src/graphemebreak.c",
        "src/graphemebreakdata.c",
        "src/emojidef.c",
        "src/eastasianwidthdef.c",
        "src/emojidata.c",
        "src/eastasianwidthdata.c",
        "src/wordbreak.c",
        "src/wordbreakdata.c",
    ]
    thirdparty_sources = [thirdparty_dir + file for file in thirdparty_sources]

    env_unicode.Prepend(CPPPATH=[thirdparty_dir + "/src"])
    env.Prepend(CPPPATH=[thirdparty_dir + "/src"])

    env_thirdparty = env_unicode.Clone()
    env_thirdparty.disable_warnings()
    env_thirdparty.add_source_files(thirdparty_obj, thirdparty_sources)

    env.drivers_sources += thirdparty_obj


# Godot source files

driver_obj = []

env_unicode.add_source_files(driver_obj, "*.cpp")
env.drivers_sources += driver_obj

# Needed to force rebuilding the driver files when the thirdparty code is updated.
env.Depends(driver_obj, thirdparty_obj)
